buildscript {
	ext {
		kotlinVersion = '1.3.10'
		springBootVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath("net.researchgate:gradle-release:2.7.0")
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
	}
}

plugins {
	id "com.jfrog.bintray" version "1.8.4"
	id "maven-publish"
	id 'com.gradle.build-scan' version '1.16'
	id "com.google.protobuf" version "0.8.7"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.researchgate.release'
apply plugin: 'maven'

group = 'vadc'
sourceCompatibility = 1.11

ext {
	bintrayUser = "$System.env.BINTRAY_USER"
	bintrayKey = "$System.env.BINTRAY_KEY"
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	implementation (
		'org.springframework.boot:spring-boot-starter-web',
		'org.springframework.data:spring-data-redis',
		'org.springframework.boot:spring-boot-starter-actuator',
		'org.springframework:spring-jms',
		'com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.0',
		'redis.clients:jedis:2.9.0',
		'org.jetbrains.kotlin:kotlin-stdlib-jdk8',
		'org.jetbrains.kotlin:kotlin-reflect',
		'com.fasterxml.jackson.module:jackson-module-kotlin',
		'com.amazonaws:aws-java-sdk:1.11.461',
		'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.4',
		'org.json:json:20180813', 
		'io.github.lognet:grpc-spring-boot-starter:3.0.0'
	)
	testImplementation (
		'org.springframework.boot:spring-boot-starter-test',
		'org.testcontainers:testcontainers:1.10.1',
		'io.rest-assured:rest-assured:3.2.0',
		'io.rest-assured:json-schema-validator:3.2.0',
		'org.awaitility:awaitility:3.1.3',
		'org.mockito:mockito-core:2.23.4'
	)
}

wrapper {
	gradleVersion = '4.10.2'
}

//release
afterReleaseBuild.dependsOn bintrayUpload

def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "vadc"
			name "Vadym Chekrii"
			email "vadym.chekrii@gmail.com"
		}
	}

	scm {
		url "https://github.com/vadymc/HeartBeatFunnel"
	}
}

publishing {
	publications {
		HeartBeatFunnel(MavenPublication) {
			from components.java
			groupId 'vadc'
			artifactId 'HeartBeatFunnel'
			version "$project.version"
			pom.withXml {
				def root = asNode()
				root.appendNode('name', 'HeartBeat Funnel')
				root.appendNode('url', 'https://github.com/vadymc/HeartBeatFunnel')
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = bintrayUser
	key = bintrayKey
	publications=['HeartBeatFunnel']
	publish = true
	pkg {
		repo = 'heartbeat'
		name = 'heartBeatFunnel'
		userOrg = 'vadc'
		vcsUrl = 'https://github.com/vadymc/HeartBeatFunnel.git'
		licenses = ['Apache-2.0']
		version {
			name = "$project.version"
			desc = "$project.version"
			released  = new Date()
		}
	}
}

release {
	failOnUnversionedFiles = false
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.6.1'
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.17.1"
		}
	}

	generateProtoTasks {
		ofSourceSet('main').each { task ->
			task.builtins {
				java{
					outputSubDir = 'protoGen'
				}
			}
			task.plugins {
				grpc {
					outputSubDir = 'protoGen'
				}
			}
		}
	}
	generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen{
	doFirst{
		delete("$projectDir/src/main/protoGen")
	}
}
clean.dependsOn cleanProtoGen

sourceSets {
	main {
		java {
			srcDir 'src/main/protoGen'
		}
	}
}

